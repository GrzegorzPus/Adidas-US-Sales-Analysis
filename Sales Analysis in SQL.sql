USE AdidasSales
GO

-- Import data:

CREATE TABLE SalesData (
    Retailer NVARCHAR(255),
    RetailerID INT,
    InvoiceDate DATE,
    Region NVARCHAR(255),
    State NVARCHAR(100),
    City NVARCHAR(100),
    Product NVARCHAR(255),
    PricePerUnit DECIMAL(10, 2),
    UnitsSold INT,
    TotalSales DECIMAL(15, 2),
    OperatingProfit DECIMAL(15, 2),
    OperatingMargin DECIMAL(5, 2),
    SalesMethod NVARCHAR(50)
);

BULK INSERT SalesData
FROM 'C:\Users\GRZEGORZ\Downloads\Adidas US Sales Dataset.csv'
WITH 
(
    FIELDTERMINATOR = ',',
    ROWTERMINATOR = '\n',
    FIRSTROW = 2,
    TABLOCK
);

/* General Sales Analysis:
 1. What is the total sales generated across all retailers? */

SELECT
	SUM(TotalSales) AS 'Total Sales'
FROM SalesData;

-- 2. Which retailer contributes the most to total sales revenue?

SELECT
	Retailer, SUM(TotalSales) AS 'Total Sales'
FROM SalesData
GROUP BY
	Retailer
ORDER BY
	[Total Sales] DESC;

-- 3. What is the average sales revenue?

SELECT
	AVG(TotalSales) AS 'Average Sales Revenue'
FROM SalesData;

-- 4. How does the number of units sold vary by region?

SELECT
	Region,
	SUM(UnitsSold) AS 'Total Units Sold'
FROM SalesData
GROUP BY
	Region
ORDER BY
	[Total Units Sold] DESC;

-- 5. Which city generates the highest total sales?

SELECT TOP 1
	City,
	SUM(TotalSales) AS 'Total Sales'
FROM SalesData
GROUP BY
	City
ORDER BY
	[Total Sales] DESC;

-- 6. How many unique retailers are present in the dataset?

SELECT
	COUNT(DISTINCT Retailer) AS 'Unique Retailers'
FROM SalesData;

-- 7. What is the most sold product category by volume?

CREATE TABLE #TempSalesData (
    Retailer NVARCHAR(255),
    RetailerID INT,
    InvoiceDate DATE,
    Region NVARCHAR(255),
    State NVARCHAR(100),
    City NVARCHAR(100),
    Product NVARCHAR(255),
    PricePerUnit DECIMAL(10, 2),
    UnitsSold INT,
    TotalSales DECIMAL(15, 2),
    OperatingProfit DECIMAL(15, 2),
    OperatingMargin DECIMAL(5, 2),
    SalesMethod NVARCHAR(50),
    Category NVARCHAR(255)
);

INSERT INTO #TempSalesData
    (Retailer, RetailerID, InvoiceDate, Region, State, City, Product, PricePerUnit, UnitsSold, TotalSales, OperatingProfit, OperatingMargin, SalesMethod, Category)
SELECT
    Retailer, RetailerID, InvoiceDate, Region, State, City, Product, PricePerUnit, UnitsSold, TotalSales, OperatingProfit, OperatingMargin, SalesMethod,
    CASE
        WHEN CHARINDEX('s', Product) > 0
        THEN LTRIM(SUBSTRING(Product, CHARINDEX('s', Product) + 1, LEN(Product)))
        ELSE NULL
    END AS Category
FROM SalesData;

SELECT TOP 1
	Category,
	SUM(UnitsSold) AS 'Total Units Sold' 
	FROM #TempSalesData
GROUP BY
	Category
ORDER BY
	[Total Units Sold] DESC;

-- 8. What is the total sales revenue for each region?

SELECT
	Region,
	SUM(TotalSales) AS 'Total Sales' 
FROM SalesData
GROUP BY
	Region
ORDER BY
	[Total Sales] DESC;

-- 9. How does sales revenue vary by product category?

SELECT
	Category,
	SUM(TotalSales) AS 'Total Sales'
FROM #TempSalesData
GROUP BY
	Category
ORDER BY
	[Total Sales] DESC;

-- 10. What are the top three cities in terms of total sales?

SELECT TOP 3
	City,
	SUM(TotalSales) AS 'Total Sales'
FROM SalesData
GROUP BY
	City
ORDER BY
	[Total Sales] DESC;

-- 11. Which region contributes the most to profit?

SELECT
	Region,
	SUM(OperatingProfit) AS 'Total Profit'
FROM SalesData
GROUP BY
	Region
ORDER BY
	[Total Profit] DESC;

-- 12. What is the percentage of sales generated by men's products vs. women's products?

ALTER TABLE SalesData
ADD Gender NVARCHAR(255),
    Category NVARCHAR(255);

UPDATE SalesData
SET 
    Gender = CASE
        WHEN CHARINDEX('s', Product) > 0
        THEN REPLACE(LTRIM(SUBSTRING(Product, 1, CHARINDEX('s', Product) - 1)), '''', '')
        ELSE NULL
    END,
    Category = CASE
        WHEN CHARINDEX('s', Product) > 0
        THEN LTRIM(SUBSTRING(Product, CHARINDEX('s', Product) + 1, LEN(Product)))
        ELSE NULL
    END;

SELECT 
    SUM(CASE WHEN Gender = 'Men' THEN TotalSales ELSE 0 END) * 100.0 / NULLIF(SUM(TotalSales), 0) AS 'Percentage Men Sales',
    SUM(CASE WHEN Gender = 'Women' THEN TotalSales ELSE 0 END) * 100.0 / NULLIF(SUM(TotalSales), 0) AS 'Percentage Women Sales'
FROM SalesData;

-- 13. What is the trend in sales revenue over time?

SELECT
	InvoiceDate,
	SUM(TotalSales) AS 'Total Sales'
FROM SalesData
GROUP BY
	InvoiceDate
ORDER BY
	InvoiceDate;

-- 14. What is the average total sales per retailer?

SELECT
	Retailer,
	ROUND(AVG(TotalSales),2) AS 'Average Sales'
FROM SalesData
GROUP BY
	Retailer
ORDER BY
	[Average Sales] DESC;

-- 15. Which state generates the highest total sales?

SELECT TOP 1
	State,
	SUM(TotalSales) AS 'TotaL Sales'
FROM SalesData
GROUP BY
	State
ORDER BY
	[Total Sales] DESC;

-- 16. What is the distribution of sales across different sales methods (in-store vs. online)?

SELECT
	SalesMethod,
	SUM(TotalSales) AS 'Total Sales' 
FROM SalesData
WHERE
	SalesMethod IN ('In-store', 'Online')
GROUP BY
	SalesMethod
ORDER BY
	[Total Sales] DESC;

-- 17. Which product category has the highest average operating margin?

SELECT TOP 1
	Category,
	AVG(OperatingMargin) AS 'Average Operating Margin'
FROM SalesData
GROUP BY
	Category
ORDER BY
	[Average Operating Margin] DESC;

-- 18. What is the total number of units sold across all retailers?

SELECT
	Retailer,
	SUM(UnitsSold) AS 'Total Units Sold'
FROM SalesData
GROUP BY
	Retailer
ORDER BY
	[Total Units Sold] DESC;

-- 19. How does total sales compare between footwear and apparel?

SELECT
	CASE
		WHEN Category IN ('Street Footwear', 'Athletic Footwear') THEN 'Footwear'
		ELSE 'Apparel'
	END AS ProductType,
	SUM(TotalSales) AS 'Total Sales' FROM SalesData
GROUP BY
	CASE
		WHEN Category IN ('Street Footwear', 'Athletic Footwear') THEN 'Footwear'
		ELSE 'Apparel'
	END;

-- 20. What is the highest total sales recorded for a single invoice?

SELECT * 
FROM SalesData
WHERE
	TotalSales = (SELECT MAX(TotalSales) FROM SalesData);

-- 21. How many unique products are sold across all retailers?

SELECT
	Retailer,
	COUNT(DISTINCT Product) AS 'Unique Products'
FROM SalesData
GROUP BY
	Retailer;

-- 22. What is the most profitable region in terms of profit?

SELECT TOP 1
	Region,
	SUM(OperatingProfit) AS 'Profit' 
FROM SalesData
GROUP BY
	Region
ORDER BY
	[Profit] DESC;

-- 23. What is the total number of invoices for each retailer?

SELECT
	Retailer,
	COUNT(InvoiceDate) AS 'Number of Invoices'
FROM SalesData
GROUP BY
	Retailer
ORDER BY
	[Number of Invoices] DESC;

-- 24. What is the overall percentage contribution of each product category to total sales?

SELECT 
    SUM(CASE WHEN Category = 'Street Footwear' THEN TotalSales ELSE 0 END) * 100.0 / NULLIF(SUM(TotalSales), 0) AS 'Percentage Street Footwear',
    SUM(CASE WHEN Category = 'Athletic Footwear' THEN TotalSales ELSE 0 END) * 100.0 / NULLIF(SUM(TotalSales), 0) AS 'Percentage Athletic Footwear',
	SUM(CASE WHEN Category = 'Apparel' THEN TotalSales ELSE 0 END) * 100.0 / NULLIF(SUM(TotalSales), 0) AS 'Percentage Apparel'
FROM SalesData;

/* Profit and Margin Analysis:
25. What is the total operating profit generated across all retailers? */

SELECT
	Retailer,
	SUM(OperatingProfit) AS 'Profit'
FROM SalesData
GROUP BY
	Retailer
ORDER BY
	[Profit] DESC;

-- 26. Which retailer has the highest total operating profit?

SELECT TOP 1
	Retailer,
	SUM(OperatingProfit) AS 'Profit'
FROM SalesData
GROUP BY
	Retailer
ORDER BY
	[Profit] DESC;

-- 27. What is the average operating margin for each product category?

SELECT
	Category,
	AVG(OperatingMargin) AS 'Average Operating Margin'
FROM SalesData
GROUP BY
	Category
ORDER BY
	[Average Operating Margin] DESC;

-- 28. How does operating profit vary by sales method?

SELECT
	SalesMethod,
	SUM(OperatingProfit) AS 'Profit'
FROM SalesData
GROUP BY
	SalesMethod
ORDER BY
	[Profit] DESC;

-- 29. What is the highest operating margin recorded for a single transaction?

SELECT * 
FROM SalesData
WHERE
	OperatingMargin = (SELECT MAX(OperatingMargin) FROM SalesData);

-- 30. How does the operating profit differ across different states?

SELECT
	State,
	SUM(OperatingProfit) AS 'Profit'
FROM SalesData
GROUP BY
	State
ORDER BY
	[Profit] DESC;

-- 31. Which product category has the lowest average operating margin?

SELECT TOP 1
	Category,
	AVG(OperatingMargin) AS 'Average Operating Margin'
FROM SalesData
GROUP BY
	Category
ORDER BY
	[Average Operating Margin];

-- 32. What is the overall operating margin for the entire dataset?

SELECT
	AVG(OperatingMargin) AS 'Average Operating Margin'
FROM SalesData;

-- 33. What is the relationship between price per unit and operating margin?

WITH Stats AS (
    SELECT 
        AVG(PricePerUnit) AS AvgPricePerUnit,
        AVG(OperatingMargin) AS AvgOperatingMargin,
        STDEV(PricePerUnit) AS StdDevPricePerUnit,
        STDEV(OperatingMargin) AS StdDevOperatingMargin
    FROM SalesData
),
Covariance AS (
    SELECT 
        SUM((PricePerUnit - Stats.AvgPricePerUnit) * (OperatingMargin - Stats.AvgOperatingMargin)) AS CovXY,
        COUNT(*) AS n
    FROM SalesData, Stats
)
SELECT 
    CovXY / ((n - 1) * Stats.StdDevPricePerUnit * Stats.StdDevOperatingMargin) AS Correlation
FROM Covariance, Stats;

-- 34. How do operating margins vary by retailer?

SELECT
	Retailer,
	AVG(OperatingMargin) AS 'Average Operating Margin'
FROM SalesData
GROUP BY
	Retailer
ORDER BY
	[Average Operating Margin] DESC;

-- 35. What is the operating profit contribution from men’s vs. women’s products?

SELECT 
    SUM(CASE WHEN Gender = 'Men' THEN OperatingProfit ELSE 0 END) * 100.0 / NULLIF(SUM(OperatingProfit), 0) AS 'Percentage Men Profit',
    SUM(CASE WHEN Gender = 'Women' THEN OperatingProfit ELSE 0 END) * 100.0 / NULLIF(SUM(OperatingProfit), 0) AS 'Percentage Women Profit'
FROM SalesData;

-- 36. What is the average operating margin for footwear products?

SELECT
	AVG(OperatingMargin) AS 'Average Operating Margin'
FROM SalesData
WHERE
	Category LIKE '%Footwear';

-- 37. How does operating profit compare between athletic and street footwear?

SELECT
	Category,
	SUM(OperatingProfit) AS 'Profit'
FROM SalesData
WHERE
	Category != 'Apparel'
GROUP BY
	Category
ORDER BY
	[Profit] DESC;

-- 38. Which product has the highest average operating profit per unit sold?

SELECT TOP 1
	Product, AVG([Profit Per Unit]) AS 'Average Profit Per Unit' FROM 
		(SELECT
			Product,
			(OperatingProfit / NULLIF(UnitsSold,0)) AS 'Profit Per Unit'
		FROM SalesData) AS A
GROUP BY
	Product
ORDER BY
	[Average Profit Per Unit] DESC;

-- 39. How does the operating margin differ by state?

SELECT
	State,
	AVG(OperatingMargin) AS 'Average Operating Margin'
FROM SalesData
GROUP BY
	State
ORDER BY
	[Average Operating Margin] DESC;

-- 40. What is the trend in operating profit over time?

SELECT
	InvoiceDate,
	SUM(OperatingProfit) AS 'Profit'
FROM SalesData
GROUP BY
	InvoiceDate
ORDER BY
	InvoiceDate;

-- 41. How does total operating profit compare across different months?
SELECT
	Month,
	SUM(OperatingProfit) AS 'Profit' 
FROM
	(SELECT
		MONTH(InvoiceDate) AS 'Month',
		OperatingProfit
	FROM SalesData) AS MonthTable
GROUP BY
	Month
ORDER BY
	Month;

-- 42. Which retailer has the highest average operating margin?

SELECT TOP 1
	Retailer,
	AVG(OperatingMargin) AS 'Average Operating Margin' 
FROM SalesData
GROUP BY
	Retailer
ORDER BY
	[Average Operating Margin] DESC;

-- 43. What is the average operating margin for in-store vs. online sales?

SELECT
	SalesMethod,
	AVG(OperatingMargin) AS 'Average Operating Margin'
FROM SalesData
WHERE
	SalesMethod IN ('In-store', 'Online')
GROUP BY
	SalesMethod
ORDER BY
	[Average Operating Margin] DESC;

-- 44. What is the total operating profit generated by each product category?

SELECT 
	Category,
	SUM(OperatingProfit) AS 'Profit'
FROM SalesData
GROUP BY
	Category
ORDER BY
	[Profit] DESC;

-- 45. What is the lowest recorded operating margin in the dataset?

SELECT * 
FROM SalesData
WHERE
	OperatingMargin =	
		(SELECT MIN(OperatingMargin) FROM SalesData)

-- 46. How do operating margins vary across different regions?

SELECT
	Region,
	AVG(OperatingMargin) AS 'Average Operating Margin'
FROM SalesData
GROUP BY
	Region
ORDER BY
	[Average Operating Margin] DESC;

-- 47. Which retailer has the lowest average operating margin?

SELECT TOP 1
	Retailer,
	AVG(OperatingMargin) AS 'Average Operating Margin'
FROM SalesData
GROUP BY
	Retailer
ORDER BY
	[Average Operating Margin];

-- 48. What are the 10 cities with the highest average margin?

SELECT TOP 10
	City,
	AVG(OperatingMargin) AS 'Operating Margin'
FROM SalesData
GROUP BY
	City
ORDER BY
	[Operating Margin] DESC;

/* Product Performance
49. Which product has the highest number of units sold? */

SELECT TOP 1
	Product,
	SUM(UnitsSold) AS 'Total Units Sold'
FROM SalesData
GROUP BY
	Product
ORDER BY
	[Total Units Sold] DESC;

-- 50. What is the average price per unit for each product category?

SELECT
	Category,
	AVG(PricePerUnit) AS 'Average Price per Unit'
FROM SalesData
GROUP BY
	Category
ORDER BY
	[Average Price per Unit] DESC;

-- 51. How does the number of units sold vary across different product types?

SELECT
	Category,
	SUM(UnitsSold) AS 'Total Units Sold'
FROM SalesData
GROUP BY
	Category
ORDER BY
	[Total Units Sold] DESC;

-- 52. What is the relationship between product type and total sales revenue?

WITH AggregatedData AS (
    SELECT 
        Category,
        SUM(TotalSales) AS TotalSales,
        AVG(TotalSales) AS AvgSales,
        MIN(TotalSales) AS MinSales,
        MAX(TotalSales) AS MaxSales
    FROM SalesData
    GROUP BY
		Category
),
Percentile AS (
    SELECT 
        Category,
        PERCENTILE_CONT(0.25) WITHIN GROUP (ORDER BY TotalSales) OVER (PARTITION BY Category) AS [25pct],
        PERCENTILE_CONT(0.5) WITHIN GROUP (ORDER BY TotalSales) OVER (PARTITION BY Category) AS [50pct],
        PERCENTILE_CONT(0.75) WITHIN GROUP (ORDER BY TotalSales) OVER (PARTITION BY Category) AS [75pct],
        ROW_NUMBER() OVER (PARTITION BY Category ORDER BY (SELECT NULL)) AS RowNumber
    FROM SalesData
)
SELECT 
    a.Category,
    a.TotalSales,
    a.AvgSales,
    a.MinSales,
    p.[25pct],
    p.[50pct],
    p.[75pct],
    a.MaxSales
FROM AggregatedData AS a
JOIN Percentile AS p
    ON a.Category = p.Category
WHERE
	p.RowNumber = 1;

--	53. Which product category has the highest average price per unit?

SELECT TOP 1
	Category,
	AVG(PricePerUnit) AS 'Average Pice per Unit'
FROM SalesData
GROUP BY
	Category
ORDER BY
	[Average Pice per Unit] DESC;

-- 54. How does the price per unit vary by retailer?

SELECT
	Retailer,
	AVG(PricePerUnit) AS 'Average Pice per Unit'
FROM SalesData
GROUP BY
	Retailer
ORDER BY
	[Average Pice per Unit] DESC;

-- 55. Which product has the highest price per unit sold?

SELECT TOP 1
	Product,
	AVG(PricePerUnit) AS 'Average Pice per Unit'
FROM SalesData
GROUP BY
	Product
ORDER BY
	[Average Pice per Unit] DESC;

-- 56. What is the distribution of units sold across different product categories?

SELECT
	Category,
	Distribution,
	COUNT(*) AS 'Number' FROM
		(SELECT
			Category,
			CASE
				WHEN UnitsSold BETWEEN 0 AND 199 THEN '0-199'
				WHEN UnitsSold BETWEEN 200 AND 399 THEN '200-399'
				WHEN UnitsSold BETWEEN 400 AND 599 THEN '400-599'
				WHEN UnitsSold BETWEEN 600 AND 799 THEN '600-799'
				WHEN UnitsSold BETWEEN 800 AND 999 THEN '800-999'
				WHEN UnitsSold BETWEEN 1000 AND 1199 THEN '1000-1199'
				ELSE 'Over 1200' END AS 'Distribution'
		FROM SalesData) AS Distribution
GROUP BY
	Category,
	Distribution
ORDER BY
	Category,
	Distribution;

-- 57. Which month was the most profitable?

SELECT TOP 1
	DATENAME(MONTH, InvoiceDate) AS 'Month',
	SUM(OperatingProfit) AS 'Total Profit' 
FROM SalesData
GROUP BY
	DATENAME(MONTH, InvoiceDate)
ORDER BY
	[Total Profit] DESC;

-- 58. How do men’s and women’s product sales compare in terms of units sold?

SELECT
	Gender,
	SUM(UnitsSold) AS 'Total Units Sold'
FROM SalesData
GROUP BY
	Gender
ORDER BY
	[Total Units Sold] DESC;

-- 59. Which product has the lowest total sales revenue?

SELECT TOP 1
	Product,
	SUM(TotalSales) AS 'Total Sales'
FROM SalesData
GROUP BY
	Product
ORDER BY
	[Total Sales];

-- 60. What is the highest number of units sold in a single transaction?

SELECT * 
FROM SalesData
WHERE
	UnitsSold IN (
		SELECT MAX(UnitsSold) FROM SalesData);

-- 61. How do price per unit and units sold correlate across product categories?

WITH Stats AS (
    SELECT 
        AVG(PricePerUnit) AS AvgPricePerUnit,
        AVG(UnitsSold) AS AvgUnitsSold,
        STDEV(PricePerUnit) AS StdDevPricePerUnit,
        STDEV(UnitsSold) AS StdDevUnitsSold
    FROM SalesData
),
Covariance AS (
    SELECT 
        SUM((PricePerUnit - Stats.AvgPricePerUnit) * (UnitsSold - Stats.AvgUnitsSold)) AS CovXY,
        COUNT(*) AS n
    FROM SalesData, Stats
)
SELECT 
    CovXY / ((n - 1) * Stats.StdDevPricePerUnit * Stats.StdDevUnitsSold) AS Correlation
FROM Covariance, Stats;

-- 62. What is the total number of units sold for men’s footwear?

SELECT
	SUM(UnitsSold) AS 'Units Sold for men’s footwear'
FROM SalesData
WHERE
	Gender = 'Men'
	AND Category != 'Apparel';

-- 63. What is the average price per unit for men’s vs. women’s products?

SELECT
	Gender,
	AVG(PricePerUnit) AS 'Average per Unit'
FROM SalesData
GROUP BY
	Gender
ORDER BY
	[Average per Unit] DESC;

-- 64. Which retailer sells the most athletic footwear?

SELECT
	Retailer,
	SUM(UnitsSold) AS 'Total Units Sold'
FROM SalesData
WHERE
	Category = 'Athletic Footwear'
GROUP BY
	Retailer
ORDER BY
	[Total Units Sold] DESC;

-- 65. What is the total sales revenue generated by street footwear?

SELECT
	SUM(TotalSales) AS 'Total Sales'
FROM SalesData
WHERE
	Category = 'Street Footwear';

-- 66. What is the percentage share of retailers in the sale of women's products?

SELECT
	Retailer, 
    CAST(SUM(UnitsSold) AS FLOAT) / 
		(SELECT SUM(UnitsSold) FROM SalesData WHERE Gender = 'Women') * 100 AS 'Sales Percentage'
FROM SalesData
WHERE
	Gender = 'Women'
GROUP BY
	Retailer;

-- 67. How do sales vary across different product categories over time?

SELECT
	Category,
	InvoiceDate, SUM(TotalSales) AS 'Total Sales'
FROM SalesData
GROUP BY
	Category,
	InvoiceDate
ORDER BY
	Category,
	InvoiceDate;

-- 68. Which product has the highest operating profit per unit?

SELECT TOP 1
	Product,
	AVG(OperatingMargin) AS 'Operating Marign'
FROM SalesData
GROUP BY
	Product
ORDER BY
	[Operating Marign] DESC;

-- 69. How does the price per unit change over time?

WITH DailyAvgPrice AS (
    SELECT 
		InvoiceDate,
		CAST(ROUND(AVG(PricePerUnit), 2) AS FLOAT) AS 'Price per Unit'
    FROM SalesData
    GROUP BY
		InvoiceDate
)
SELECT
    InvoiceDate,
    [Price per Unit],
    ROUND([Price per Unit] - LAG([Price per Unit]) OVER (ORDER BY InvoiceDate),2) AS 'Price Growth',
    CASE 
        WHEN LAG([Price per Unit]) OVER (ORDER BY InvoiceDate) IS NULL THEN NULL
        ELSE ROUND(([Price per Unit] - LAG([Price per Unit]) OVER (ORDER BY InvoiceDate)) / LAG([Price per Unit]) OVER (ORDER BY InvoiceDate) * 100,2)
    END AS 'Price Growth Percent'
FROM DailyAvgPrice
ORDER BY InvoiceDate;

-- 70. What is the relationship between units sold and operating profit?

WITH Stats AS (
    SELECT 
        AVG(OperatingProfit) AS AvgProfit,
        AVG(UnitsSold) AS AvgUnitsSold,
        STDEV(OperatingProfit) AS StdDevProfit,
        STDEV(UnitsSold) AS StdDevUnitsSold
    FROM SalesData
),
Covariance AS (
    SELECT 
        SUM((OperatingProfit - Stats.AvgProfit) * (UnitsSold - Stats.AvgUnitsSold)) AS CovXY,
        COUNT(*) AS n
    FROM SalesData, Stats
)
SELECT 
    CovXY / ((n - 1) * Stats.StdDevProfit * Stats.StdDevUnitsSold) AS Correlation
FROM Covariance, Stats;

-- 71. Which product category has the highest total sales in the Northeast region?

SELECT TOP 1
	Category,
	SUM(TotalSales) AS 'Total Sales'
FROM SalesData
WHERE
	Region = 'Northeast'
GROUP BY
	Category
ORDER BY
	[Total Sales] DESC;

-- 72. How does the price per unit vary by region?

SELECT
	Region,
	AVG(PricePerUnit) AS 'Average Price per Unit'
FROM SalesData
GROUP BY
	Region
ORDER BY
	[Average Price per Unit] DESC;

-- 73. Which product category has the lowest average price per unit?

SELECT TOP 1
	Category,
	AVG(PricePerUnit) AS 'Average Price per Unit'
FROM SalesData
GROUP BY
	Category
ORDER BY
	[Average Price per Unit];

-- 74. How many total units of women’s footwear were sold?

SELECT
	SUM(UnitsSold) AS 'Total Units Sold'
FROM SalesData
WHERE
	Gender = 'Women' 
	AND Category != 'Apparel';

/* Time and Seasonal Analysis
75. How do average sales vary by month of the year? */

CREATE TABLE #TimeSeriesSalesData (
    InvoiceDate DATE,
    Weekday NVARCHAR(20),
    Month NVARCHAR(20),
    Quarter NVARCHAR(2),
    Year INT,
    AvgPricePerUnit DECIMAL(18, 2),
    TotalUnitsSold INT,
    TotalSales DECIMAL(18, 2),
    TotalProfit DECIMAL(18, 2),
    AvgOperatingMargin DECIMAL(18, 2)
);

INSERT INTO #TimeSeriesSalesData
SELECT 
    [InvoiceDate],
    DATENAME(WEEKDAY, [InvoiceDate]) AS Weekday,
    DATENAME(month, [InvoiceDate]) AS Month,
    'Q' + CAST(DATEPART(quarter, [InvoiceDate]) AS VARCHAR(1)) AS Quarter,
    YEAR([InvoiceDate]) AS Year,
    AVG([PricePerUnit]) AS AvgPricePerUnit,
    SUM([UnitsSold]) AS TotalUnitsSold,
    SUM([TotalSales]) AS TotalSales,
    SUM([OperatingProfit]) AS TotalProfit,
    AVG([OperatingMargin]) AS AvgOperatingMargin
FROM SalesData
GROUP BY
    [InvoiceDate], 
    DATENAME(WEEKDAY, [InvoiceDate]),
    DATENAME(month, [InvoiceDate]),
    DATEPART(quarter, [InvoiceDate]),
    YEAR([InvoiceDate])
ORDER BY InvoiceDate;

SELECT
    Month,
    CAST(ROUND(AVG(TotalSales), 2) AS DECIMAL(10, 2)) AS TotalSales
FROM #TimeSeriesSalesData
GROUP BY
    Month,
    MONTH(CONVERT(DATE, '01-' + Month + '-2020', 105))
ORDER BY
    MONTH(CONVERT(DATE, '01-' + Month + '-2020', 105));

-- 76. What is the trend in total sales over the first quarter of 2020?

WITH SalesWithLag AS (
    SELECT 
        InvoiceDate,
        TotalSales,
        LAG(TotalSales) OVER (ORDER BY InvoiceDate) AS PreviousSales
    FROM #TimeSeriesSalesData
    WHERE 
        Quarter = 'Q1' 
        AND Year = 2020)
SELECT 
    InvoiceDate,
    TotalSales,
    PreviousSales,
    TotalSales - PreviousSales AS SalesGrowth,
    CASE 
        WHEN PreviousSales IS NULL OR PreviousSales = 0 THEN NULL 
        ELSE CAST(ROUND((TotalSales - PreviousSales) * 100.0 / PreviousSales, 2) AS DECIMAL(10, 2))
    END AS SalesGrowthPercentage
FROM SalesWithLag
ORDER BY InvoiceDate;

-- 77. How does sales revenue vary by day of the week?

SELECT 
    Weekday,
    SUM(TotalSales) AS TotalSales
FROM 
    #TimeSeriesSalesData
GROUP BY 
    Weekday
ORDER BY 
    CASE Weekday
        WHEN 'Monday' THEN 1
        WHEN 'Tuesday' THEN 2
        WHEN 'Wednesday' THEN 3
        WHEN 'Thursday' THEN 4
        WHEN 'Friday' THEN 5
        WHEN 'Saturday' THEN 6
		WHEN 'Sunday' THEN 7
    END;

-- 78. What are the peak sales periods across the dataset?

SELECT *
FROM #TimeSeriesSalesData
WHERE
	TotalSales = (
		SELECT MAX(TotalSales)
		FROM #TimeSeriesSalesData);

-- 79. How do seasonal trends affect operating margin?

SELECT 
	InvoiceDate,
	AvgOperatingMargin
FROM
	#TimeSeriesSalesData;

-- 80. Which month has the highest profit?

SELECT TOP 1
    Month,
    SUM(TotalProfit) AS 'Total Profit'
FROM #TimeSeriesSalesData
GROUP BY
    Month
ORDER BY
	[Total Profit] DESC;

-- 81. How does the number of units sold vary across different months?

SELECT
	Month,
	SUM(TotalUnitsSold) AS 'Total Units Sold'
FROM
	#TimeSeriesSalesData
GROUP BY
    Month,
    MONTH(CONVERT(DATE, '01-' + Month + '-2020', 105))
ORDER BY
    MONTH(CONVERT(DATE, '01-' + Month + '-2020', 105));

-- 82. What is the impact of seasonal trends on sales?

WITH SalesWithLag AS (
    SELECT 
        InvoiceDate,
        TotalSales,
        LAG(TotalSales) OVER (ORDER BY InvoiceDate) AS PreviousSales
    FROM #TimeSeriesSalesData)

SELECT 
    InvoiceDate,
    TotalSales,
    TotalSales - PreviousSales AS SalesGrowth,
    CASE 
        WHEN PreviousSales IS NULL OR PreviousSales = 0 THEN NULL 
        ELSE CAST(ROUND((TotalSales - PreviousSales) * 100.0 / PreviousSales, 2) AS DECIMAL(10, 2))
    END AS SalesGrowthPercentage
FROM SalesWithLag
ORDER BY InvoiceDate;

-- 83. How do monthly sales trends vary by region?

WITH SalesTrendsByRegion AS (
    SELECT
        Region,
        InvoiceDate,
        DATEPART(MONTH, InvoiceDate) AS MonthNumber,
        DATENAME(MONTH, InvoiceDate) AS MonthName,
        YEAR(InvoiceDate) AS Year,
        SUM(TotalSales) AS TotalSales
    FROM 
        SalesData
    GROUP BY
        InvoiceDate,
        Region
)
SELECT 
    Year,
    MonthName,
    SUM(TotalSales) AS TotalSales 
FROM 
    SalesTrendsByRegion
GROUP BY
    Year,
    MonthNumber,
    MonthName
ORDER BY
    Year, 
    MonthNumber;

-- 84. Which product category sees the most significant seasonal variation in sales?

SELECT 
	Category,
	InvoiceDate,
	SUM(TotalSales) AS TotalSales,
	CAST(ROUND((SUM(TotalSales) - LAG(SUM(TotalSales)) OVER (PARTITION BY Category ORDER BY InvoiceDate))/ LAG(SUM(TotalSales)) OVER (PARTITION BY Category ORDER BY InvoiceDate),2) AS decimal(10,2)) AS PercentageChange
FROM
	SalesData
GROUP BY
	Category,
	InvoiceDate;

-- 85. What is the total number of invoices per month?

SELECT
	DATENAME(MONTH, InvoiceDate) AS Month,
	COUNT(*) AS NumberOfInvoices
FROM
	SalesData
GROUP BY
    DATEPART(MONTH, InvoiceDate),
    DATENAME(MONTH, InvoiceDate)
ORDER BY
    DATEPART(MONTH, InvoiceDate);

-- 86. What is the average operating margin per quarter?

SELECT 
	Quarter,
	Year,
	CAST(ROUND(AVG(AvgOperatingMargin),2) AS decimal(10,2)) AS AverageOperatingMargin
FROM #TimeSeriesSalesData
GROUP BY
	Quarter,
	Year
ORDER BY
	Quarter,
	Year;

-- 87. How do sales for in-store purchases vary by seasons?

WITH SeasonSales AS (
	SELECT
		CASE
			WHEN MONTH(InvoiceDate) IN (12,1,2) THEN 'Winter'
			WHEN MONTH(InvoiceDate) IN (3,4,5) THEN 'Spring'
			WHEN MONTH(InvoiceDate) IN (6,7,8) THEN 'Summer'
			ELSE 'Fall'
		END AS Season,
		TotalSales	
	FROM SalesData
	WHERE 
		SalesMethod = 'In-store')
SELECT 
	Season,
	SUM(TotalSales) AS TotalSales
FROM SeasonSales
GROUP BY
	Season;

-- 88. How do sales for online purchases vary by season?

WITH SeasonSales AS (
	SELECT
		CASE
			WHEN MONTH(InvoiceDate) IN (12,1,2) THEN 'Winter'
			WHEN MONTH(InvoiceDate) IN (3,4,5) THEN 'Spring'
			WHEN MONTH(InvoiceDate) IN (6,7,8) THEN 'Summer'
			ELSE 'Fall'
		END AS Season,
		TotalSales	
	FROM SalesData
	WHERE 
		SalesMethod = 'Online')
SELECT 
	Season,
	SUM(TotalSales) AS TotalSales
FROM SeasonSales
GROUP BY
	Season;

-- 89. How does the average price per unit change over different months?

SELECT 
	Month,
	CAST(ROUND(AVG(AvgPricePerUnit),2) AS DECIMAL(10,2)) AS AveragePricePerUnit
FROM #TimeSeriesSalesData
GROUP BY
	Month
ORDER BY 
    CASE 
        WHEN Month = 'January' THEN 1
        WHEN Month = 'February' THEN 2
        WHEN Month = 'March' THEN 3
        WHEN Month = 'April' THEN 4
        WHEN Month = 'May' THEN 5
        WHEN Month = 'June' THEN 6
        WHEN Month = 'July' THEN 7
        WHEN Month = 'August' THEN 8
        WHEN Month = 'September' THEN 9
        WHEN Month = 'October' THEN 10
        WHEN Month = 'November' THEN 11
        WHEN Month = 'December' THEN 12
    END;

-- 90. What is the trend in average operating profit across different quarters?

SELECT 
	Quarter,
	Year,
	CAST(ROUND(AVG(TotalProfit),2) AS decimal(10,2)) AS AvgProfit
FROM
	#TimeSeriesSalesData
GROUP BY 
	Quarter,
	Year;

-- 91. What is the seasonal trend for men’s products sales?

SELECT 
	InvoiceDate,
	TotalSales,
	TotalSales - LAG(TotalSales) OVER (ORDER BY InvoiceDate) AS Change,
	CAST(ROUND((TotalSales - LAG(TotalSales) OVER (ORDER BY InvoiceDate)) / LAG(TotalSales) OVER (ORDER BY InvoiceDate),2) AS decimal(10,2)) AS PercentageChange
FROM SalesData
WHERE
	Gender = 'Men'
ORDER BY
	InvoiceDate;

-- 92. Which month has the highest average operating margin?

SELECT
	Month,
	ROUND(AVG(AvgOperatingMargin),2) AS AvgOperatingMargin
FROM
	#TimeSeriesSalesData
GROUP BY
	Month
ORDER BY 
    CASE 
        WHEN Month = 'January' THEN 1
        WHEN Month = 'February' THEN 2
        WHEN Month = 'March' THEN 3
        WHEN Month = 'April' THEN 4
        WHEN Month = 'May' THEN 5
        WHEN Month = 'June' THEN 6
        WHEN Month = 'July' THEN 7
        WHEN Month = 'August' THEN 8
        WHEN Month = 'September' THEN 9
        WHEN Month = 'October' THEN 10
        WHEN Month = 'November' THEN 11
        WHEN Month = 'December' THEN 12
    END;

-- 93. What are the peak sales months for each retailer?

WITH PeakSales AS (
SELECT 
    Retailer,
    YEAR(InvoiceDate) AS Year,
	DATENAME(MONTH, InvoiceDate) AS Month,
    SUM(TotalSales) AS TotalSales,
	RANK() OVER (PARTITION BY Retailer ORDER BY SUM(TotalSales)) AS Peak
FROM 
    SalesData
GROUP BY
    Retailer,
    DATENAME(MONTH, InvoiceDate),
    MONTH(InvoiceDate),
    YEAR(InvoiceDate)
)
SELECT 
	Retailer,
	Year,
	Month,
	TotalSales
FROM
	PeakSales
WHERE Peak = 1
ORDER BY TotalSales DESC;

-- 94. What is the overall sales trend during the winter months?

SELECT 
	InvoiceDate,
	TotalSales,
	TotalSales - LAG(TotalSales) OVER (ORDER BY InvoiceDate) AS Change,
	CAST(ROUND((TotalSales - LAG(TotalSales) OVER (ORDER BY InvoiceDate)) / LAG(TotalSales) OVER (ORDER BY InvoiceDate),2) AS decimal(10,2)) AS PercentageChange
FROM
	#TimeSeriesSalesData
WHERE
	MONTH(InvoiceDate) IN (12,1,2)
ORDER BY
	InvoiceDate;

-- 95. How do sales methods vary by seasons?

WITH Seasson AS (
SELECT 
	SalesMethod,
	CASE
		WHEN MONTH(InvoiceDate) IN (12,1,2) THEN 'Winter'
		WHEN MONTH(InvoiceDate) IN (3,4,5) THEN 'Spring'
		WHEN MONTH(InvoiceDate) IN (6,7,8) THEN 'Summer'
		ELSE 'Fall'
	END AS Seasson,
	TotalSales
FROM SalesData
)
SELECT
	SalesMethod,
	Seasson,
	SUM(TotalSales) AS TotalSales
FROM 
	Seasson
GROUP BY
	SalesMethod,
	Seasson
ORDER BY
	SalesMethod,
    CASE 
        WHEN Seasson = 'Spring' THEN 1
        WHEN Seasson = 'Summer' THEN 2
        WHEN Seasson = 'Fall' THEN 3
        WHEN Seasson = 'Winter' THEN 4
	END;

-- 96. What is the trend in women’s footwear sales over time?

SELECT 
    InvoiceDate,
    TotalSales,
    TotalSales - LAG(TotalSales) OVER (ORDER BY InvoiceDate) AS Change,
    CASE 
        WHEN LAG(TotalSales) OVER (ORDER BY InvoiceDate) IS NULL OR LAG(TotalSales) OVER (ORDER BY InvoiceDate) = 0 THEN NULL
        ELSE CAST(ROUND((TotalSales - LAG(TotalSales) OVER (ORDER BY InvoiceDate)) / LAG(TotalSales) OVER (ORDER BY InvoiceDate), 2) AS decimal(10,2))
    END AS PercentageChange
FROM SalesData
WHERE
    Gender = 'Women' AND Category != 'Apparel';

-- 97. How do products category vary by seasons?

WITH Seasson AS (
SELECT 
	Category,
	CASE
		WHEN MONTH(InvoiceDate) IN (12,1,2) THEN 'Winter'
		WHEN MONTH(InvoiceDate) IN (3,4,5) THEN 'Spring'
		WHEN MONTH(InvoiceDate) IN (6,7,8) THEN 'Summer'
		ELSE 'Fall'
	END AS Seasson,
	TotalSales
FROM SalesData
)
SELECT
	Category,
	Seasson,
	SUM(TotalSales) AS TotalSales
FROM 
	Seasson
GROUP BY
	Category,
	Seasson
ORDER BY
	Category,
    CASE 
        WHEN Seasson = 'Spring' THEN 1
        WHEN Seasson = 'Summer' THEN 2
        WHEN Seasson = 'Fall' THEN 3
        WHEN Seasson = 'Winter' THEN 4
	END;

-- 98. How do profit for in-store purchases vary by day of week?

SELECT
	DATENAME(WEEKDAY, InvoiceDate) AS Weekday,
	SUM(OperatingProfit) AS TotalProfit
FROM SalesData
WHERE
	SalesMethod = 'In-store'
GROUP BY
	SalesMethod,
	DATENAME(WEEKDAY, InvoiceDate)
ORDER BY 
    CASE DATENAME(WEEKDAY, InvoiceDate)
        WHEN 'Monday' THEN 1
        WHEN 'Tuesday' THEN 2
        WHEN 'Wednesday' THEN 3
        WHEN 'Thursday' THEN 4
        WHEN 'Friday' THEN 5
        WHEN 'Saturday' THEN 6
		WHEN 'Sunday' THEN 7
    END;
	
-- 99. How do profit vary over time in state with most total sales?

SELECT
	InvoiceDate,
	SUM(OperatingProfit) AS Profit,
	    SUM(OperatingProfit) - LAG(SUM(OperatingProfit)) OVER (ORDER BY InvoiceDate) AS Change,
    CASE 
        WHEN LAG(SUM(OperatingProfit)) OVER (ORDER BY InvoiceDate) IS NULL OR LAG(SUM(OperatingProfit)) OVER (ORDER BY InvoiceDate) = 0 THEN NULL
        ELSE CAST(ROUND((SUM(OperatingProfit) - LAG(SUM(OperatingProfit)) OVER (ORDER BY InvoiceDate)) / LAG(SUM(OperatingProfit)) OVER (ORDER BY InvoiceDate), 2) AS decimal(10,2))
    END AS PercentageChange
FROM
	SalesData
WHERE
	State = (
SELECT TOP 1
	State
FROM
	SalesData
GROUP BY
	State
ORDER BY 
	SUM(TotalSales) DESC)
GROUP BY
	InvoiceDate
ORDER BY InvoiceDate;

-- 100. Compare profit in 2020 to 2021.

SELECT
	YEAR(InvoiceDate) AS Year,
	SUM(OperatingProfit) AS Profit
FROM
	SalesData
GROUP BY
	YEAR(InvoiceDate)
ORDER BY
	YEAR(InvoiceDate);

-- Bonus: Explain the difference

SELECT
	YEAR(InvoiceDate) AS Year,
	SUM(UnitsSold) AS TotalUnitsSold,
	CAST(ROUND(AVG(PricePerUnit),2) AS DECIMAL(10,2)) AS AvgPricePerUnit,
	AVG(OperatingMargin) AS AvgOperatingMargin
FROM SalesData
GROUP BY
	YEAR(InvoiceDate)
ORDER BY
	YEAR(InvoiceDate);